<b>CS 61A&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp; Lecture Notes&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp; Week 6</b>
<div class="p"><!----></div>
Topic: Generic operators
<div class="p"><!----></div>
<b>Reading:</b>
Abelson &amp; Sussman, Sections 2.4 through 2.5.2 (pages 169-200)
<div class="p"><!----></div>
The overall problem we're addressing this week is to control the complexity
of large systems with many small procedures that handle several types of
data.  We are building toward the idea of <i>object-oriented programming</i>,
which many people see as the ultimate solution to this problem, and which we
discuss for two weeks starting next week.

<div class="p"><!----></div>
<table border="0"><tr><td>&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<a href="note-figs/7-1.eps">Figure</a>


</td></tr></table><!--vbox-->Big ideas:

<div class="p"><!----></div>

<dl compact="compact">
 <dt>
<font face="symbol"></font
></dt>
<dd>
tagged data
</dd>
 <dt>
<font face="symbol"></font
></dt>
<dd>data-directed programming
</dd>
 <dt>
<font face="symbol"></font
></dt>
<dd>message passing
</dd></dl>The first problem is keeping track of types of data.  If we see a pair whose
<tt>car</tt> is 3 and whose <tt>cdr</tt> is 4, does that represent [3/4] or
does it represent 3+4i?

<div class="p"><!----></div>
The solution is <i>tagged data </i>:  Each datum carries around its own
type information.  In effect we do <tt>(cons 'rational (cons&nbsp;3&nbsp;4))</tt> for the
rational number [3/4], although of course we use an ADT.

<div class="p"><!----></div>
Just to get away from the arithmetic examples in the text, we'll use another
example about geometric shapes.  Our data types will be squares and circles;
our operations will be area and perimeter.

<div class="p"><!----></div>
We want to be able to say, e.g., <tt>(area&nbsp;circle3)</tt> to get the area of a
particular (previously defined) circle.  To make this work, the function
<tt>area</tt> has to be able to tell which type of shape it's seeing.  We
accomplish this by attaching a type tag to each shape:

<div class="p"><!----></div>
<tt>  <pre>;;;;;                        In file cs61a/lectures/2.4/geom.scm
(define pi 3.141592654)

<div class="p"><!----></div>
(define (make-square side)
  (attach-tag 'square side))

<div class="p"><!----></div>
(define (make-circle radius)
  (attach-tag 'circle radius))

<div class="p"><!----></div>
(define (area shape)
  (cond ((eq? (type-tag shape) 'square)
         (* (contents shape) (contents shape)))
        ((eq? (type-tag shape) 'circle)
         (* pi (contents shape) (contents shape)))
        (else (error &#220;nknown shape - AREA"))))

<div class="p"><!----></div>
(define (perimeter shape)
  (cond ((eq? (type-tag shape) 'square)
         (* 4 (contents shape)))
        ((eq? (type-tag shape) 'circle)
         (* 2 pi (contents shape)))
        (else (error &#220;nknown shape - PERIMETER"))))

<div class="p"><!----></div>
;; some sample data
(define square5 (make-square 5))
(define circle3 (make-circle 3))

</pre></tt>

<div class="p"><!----></div>

<div class="p"><!----></div>
<font face="symbol"></font
> Orthogonality of types and operators.

<div class="p"><!----></div>
The next problem to deal with is the proliferation of functions because you
want to be able to apply every operation to every type.  In our example,
with two types and two operations we need four algorithms.

<div class="p"><!----></div>
What happens when we invent a new type?  If we write our program in the
<i>conventional </i> (i.e., old-fashioned) style as above, it's not enough
to add new functions; we have to modify all the operator functions like <tt>
area</tt> to know about the new type.  We'll look at two different approaches to
organizing things better: <i>data-directed programming </i> and <i>
message passing</i>.

<div class="p"><!----></div>
The idea in DDP is that instead of keeping the information about types
versus operators inside functions, as <tt>cond</tt> clauses, we record this
information in a data structure.  A&amp;S provide tools <tt>put</tt> to set up the
data structure and <tt>get</tt> to examine it:

<div class="p"><!----></div>
<tt>  <pre>&#62; (get 'foo 'baz)
#f
&#62; (put 'foo 'baz 'hello)
&#62; (get 'foo 'baz)
hello

</pre></tt>

<div class="p"><!----></div>
Once you <tt>put</tt> something in the table, it stays there.  (This is our
first departure from functional programming.  But our intent is to set up
the table at the beginning of the computation and then to treat it as
<i>constant </i> information, not as something that might be different the
next time you call <tt>get</tt>, despite the example above.)  For now we take
<tt>put</tt> and <tt>get</tt> as primitives; we'll see how to build them in
section 3.3 in three weeks.

<div class="p"><!----></div>
The code is mostly unchanged from the conventional version; the tagged
data ADT and the two shape ADTs are unchanged.  What's different is how we
represent the four algorithms for applying some operator to some type:

<div class="p"><!----></div>
<tt>  <pre>;;;;;                        In file cs61a/lectures/2.4/geom.scm

<div class="p"><!----></div>
(put 'square 'area (lambda (s) (* s s)))
(put 'circle 'area (lambda (r) (* pi r r)))
(put 'square 'perimeter (lambda (s) (* 4 s)))
(put 'circle 'perimeter (lambda (r) (* 2 pi r)))

</pre></tt>

<div class="p"><!----></div>
Notice that the entry in each cell of the table is a <i>function</i>, not a
symbol.  We can now redefine the six generic operators ("generic" because
they work for any of the types):

<div class="p"><!----></div>
<tt>  <pre>;;;;;                        In file cs61a/lectures/2.4/geom.scm

<div class="p"><!----></div>
(define (area shape)
  (operate 'area shape))

<div class="p"><!----></div>
(define (perimeter shape)
  (operate 'perimeter shape))

<div class="p"><!----></div>
(define (operate op obj)     ;; like APPLY-GENERIC but for one operand
  (let ((proc (get (type-tag obj) op)))
    (if proc
        (proc (contents obj))
        (error &#220;nknown operator for type"))))

</pre></tt>

<div class="p"><!----></div>
Now if we want to invent a new type, all we have to do is a few <tt>put</tt>
instructions and the generic operators just automatically work with the new
type.

<div class="p"><!----></div>

<div class="p"><!----></div>
Don't get the idea that DDP just means a two-dimensional table of operator
and type names!  DDP is a very general, great idea.  It means putting the
details of a system into data, rather than into programs, so you can write
general programs instead of very specific ones.

<div class="p"><!----></div>
In the old days, every time a company got a computer they had to hire a
bunch of programmers to write things like payroll programs for them.  They
couldn't just use someone else's program because the details would be
different, e.g., how many digits in the employee number.  These days you
have general business packages and each company can "tune" the program to
their specific purpose with a data file.

<div class="p"><!----></div>
Another example showing the generality of DDP is the <i>compiler compiler</i>.
It used to be that if you wanted to invent a new programming language you
had to start from scratch in writing a compiler for it.  But now we have
formal notations for expressing the syntax of the language.  (See section
7.1, page 38, of the <i>Scheme Report </i> at the back of the course reader.)
A single program can read these formal descriptions and compile any language.
[The Scheme BNF is in <tt>cs61a/lectures/2.4/bnf</tt>.]

<div class="p"><!----></div>
<font face="symbol"></font
> Message-passing.

<div class="p"><!----></div>
In conventional style, the operators are represented as functions that know
about the different types; the types themselves are just data.  In DDP, the
operators and types are all data, and there is one universal <tt>operate</tt>
function that does the work.  We can also stand conventional style on its
head, representing the <i>types </i> as functions and the operations as mere
data.

<div class="p"><!----></div>
In fact, not only are the types functions, but so are the individual data
themselves.  That is, there is a function (<tt>make-circle</tt> below) that
represents the circle type, and when you invoke that function, it
returns <i>a function </i> that represents the particular circle you give it
as its argument.  Each circle is an <i>object </i> and the function that
represents it is a <i>dispatch procedure </i> that takes as its argument a
<i>message </i> saying which operation to perform.

<div class="p"><!----></div>
<tt>  <pre>;;;;;                        In file cs61a/lectures/2.4/geom.scm

<div class="p"><!----></div>
(define (make-square side)
  (lambda (message)
    (cond ((eq? message 'area)
           (* side side))
          ((eq? message 'perimeter)
           (* 4 side))
          (else (error &#220;nknown message")))))

<div class="p"><!----></div>
(define (make-circle radius)
  (lambda (message)
    (cond ((eq? message 'area)
           (* pi radius radius))
          ((eq? message 'perimeter)
           (* 2 pi radius))
          (else (error &#220;nknown message")))))

<div class="p"><!----></div>
(define square5 (make-square 5))
(define circle3 (make-circle 3))

</pre></tt>

<div class="p"><!----></div>

<div class="p"><!----></div>
The <tt>define</tt>s that produce the individual shapes look no different
from before, but the results are different:  Each shape is a function, not a
list structure.  So to get the area of the shape <tt>circle3</tt> we
invoke that shape with the proper message:&nbsp;<tt>(circle3&nbsp;'area)</tt>.  That notation
is a little awkward so we provide a little "syntactic sugar" that allows
us to say <tt>(area&nbsp;circle3)</tt> as in the past:

<div class="p"><!----></div>
<tt>  <pre>;;;;;                        In file cs61a/lectures/2.4/msg.scm
(define (operate op obj)
  (obj op))

<div class="p"><!----></div>
(define (area shape)
  (operate 'area shape))

<div class="p"><!----></div>
(define (perimeter shape)
  (operate 'perimeter shape))

</pre></tt>

<div class="p"><!----></div>
Message passing may seem like an overly complicated way to handle this
problem of shapes, but we'll see next week that it's one of the
key ideas in creating object-oriented programming.  Message passing becomes
much more powerful when combined with the idea of <i>local state </i> that
we'll learn next week.

<div class="p"><!----></div>
We seem to have abandoned tagged data; every shape type is just some
function, and it's hard to tell which type of shape a given function
represents.  We could combine message passing with tagged data, if
desired, by adding a <tt>type</tt> message that each object understands.

<div class="p"><!----></div>
<tt>  <pre>(define (make-square side)
  (lambda (message)
    (cond ((eq? message 'area)
           (* side side))
          ((eq? message 'perimeter)
           (* 4 side))
          ((EQ? MESSAGE 'TYPE) 'SQUARE)
          (else (error &#220;nknown message")))))

</pre></tt>

<div class="p"><!----></div>
<font face="symbol"></font
> Dyadic operations.

<div class="p"><!----></div>
Our shape example is easier than the arithmetic example in the
book because our operations only require one operand, not two.  For
arithmetic operations like <tt>+</tt>, it's not good enough to connect the
operation with a type; the two operands might have two different types.
What should you do if you have to add a rational number to a complex number?

<div class="p"><!----></div>
There is no perfect solution to this problem.  For the particular case of
arithmetic, we're lucky in that the different types form a sequence of
larger and larger sets.  Every integer is a rational number; every rational
is a real; every real is a complex.  So we can deal with type mismatch by
<i>raising </i> the less-complicated operand to the type of the other one.
To add a rational number to a complex number, raise the rational number to
complex and then you're left with the problem of adding two complex numbers.
So we only need N addition algorithms, not N<sup>2</sup> algorithms, where N is
the number of types.

<div class="p"><!----></div>
Do we need N<sup>2</sup> raising algorithms?  No, because we don't have to know
directly how to raise a rational number to complex.  We can raise the
rational number to the next higher type (real), and then raise that real
number to complex.  So if we want to add [1/3] and 2+5i the answer
comes out 2.3333+5i.

<div class="p"><!----></div>
As this example shows, nonchalant raising can lose information.  It would
be better, perhaps, if we could get the answer <sup>7</sup>/<sub>3</sub>+5i instead of
the decimal approximation.  Numbers are a rat's nest full of traps for the
unwary.  You will live longer if you only write programs about integers.

<div class="p"><!----></div>
<b>Note:</b> The second midterm exam is next week.

<div class="p"><!----></div>

<div class="p"><!----></div>
